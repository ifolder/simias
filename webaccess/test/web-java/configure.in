AC_INIT(ifolder3-web-java.pc.in)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(ifolder3-web-java, 1.3)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE
m4_pattern_allow(PKG_CONFIG)

#
# Check for a valid operating system and C# compiler
#
case $host_os in
    linux*)
        IFOLDER_OS='linux'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	PKG_CONFIG_DIR='lib'
    ;;
    darwin*)
        IFOLDER_OS='darwin'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	    PKG_CONFIG_DIR='lib'
    ;;
    cygwin*)
        IFOLDER_OS='windows'
        AC_CHECK_PROG(CSC, csc, csc)
        test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: neither csc nor mcs found in \$PATH])
	PKG_CONFIG_DIR='.'
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(IFOLDER_OS)
AC_SUBST(VERSION)
AM_CONDITIONAL(LINUX, test "$IFOLDER_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$IFOLDER_OS" = "windows")
AM_CONDITIONAL(DARWIN, test "$IFOLDER_OS" = "darwin")
#AM_CONDITIONAL(MONO, test "$CSC" = "mcs")
#AM_CONDITIONAL(DOTNET, test "$CSC" = "csc")

#
# We add $prefix to PKG_CONFIG_PATH so pkg-config will find any other
# packages 'make install'-ed to the same prefix.
#
# The default value of $prefix is not set until later in the script.
# We set the default here, if needed.
#
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix="${prefix}"

#
# Installation directory options.
#
# These are normally left unexpanded so users can "make install exec_prefix=/foo".
# However, we want them expanded.
#
bindir="${exec_prefix}/bin"
sbindir="${exec_prefix}/sbin"
libexecdir="${exec_prefix}/libexec"
datadir="${prefix}/share"
sysconfdir="${prefix}/etc"
sharedstatedir="${prefix}/com"
localstatedir="${prefix}/var"
libdir="${exec_prefix}/lib"
includedir="${prefix}/include"
oldincludedir="/usr/include"
infodir="${prefix}/info"
mandir="${prefix}/man"

#
# Configure C# compiler settings
#
case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH='$(LOG4NET_DIR):\$(srcdir)/external/'
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
EXTERNAL_DIR='$(top_srcdir)/external'

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(EXTERNAL_DIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_CLEAN_FILES='*.dbg *.pdb */*.pdb *.doc.xml */*.doc.xml *.test.xml */*.test.xml'
COMMON_DISTCLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj'
COMMON_MAINTAINER_CLEAN_FILES='Makefile.in'
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(COMMON_DISTCLEAN_FILES)
AC_SUBST(COMMON_MAINTAINER_CLEAN_FILES)

#
# Check JAVA_HOME and java -version
#
if test -z "$JAVA_HOME"; then
    AC_MSG_ERROR([JAVA_HOME must be set])
fi
echo JAVA_HOME=$JAVA_HOME
JAVA_VERSION_MESSAGE=`$JAVA_HOME/bin/java -version 2>&1`
JAVA_VERSION=`echo $JAVA_VERSION_MESSAGE | sed 's/[[^0-9]]*\([[0-9\._]]*\).*/\1/'`
echo JAVA_VERSION=$JAVA_VERSION
if ! expr "$JAVA_VERSION" : "1\.4\.2.*" >/dev/null; then
    AC_MSG_ERROR([JAVA_VERSION must be 1.4.2])
fi

#
# Set platform-specific variables
#
case $IFOLDER_OS in
    #
    # Darwin-specific configuration
    #
    darwin)
        #
        # Set variables
        #
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NUNIT="$EXTERNAL_DIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$EXTERNAL_DIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$LINUX_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="sqlite"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/darwin"
        JAVA="$JAVA_HOME/bin/java"
        JAVAC="$JAVA_HOME/bin/javac"
        JAR="$JAVA_HOME/bin/jar"
		PATH_SEP=':'
    ;;
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NUNIT="$EXTERNAL_DIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$EXTERNAL_DIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$LINUX_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="libsqlite.so"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/linux"
        JAVA="$JAVA_HOME/bin/java"
        JAVAC="$JAVA_HOME/bin/javac"
        JAR="$JAVA_HOME/bin/jar"
		PATH_SEP=':'
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        INSTALL_EXE_OPTS=''
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/net/1.0/release"
        NUNIT="$EXTERNAL_DIR/NUnit/net/bin/nunit-console.exe"
        NUNIT_LIBPATH="$EXTERNAL_DIR/NUnit/net/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$WINDOWS_SUBDIRS
        REPORT_DIR='$(shell cygpath --mixed $(DESTDIR)$(bindir))'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
		SEP='$(EMPTY)\\$(EMPTY)'
		SQLITELIB="sqlite.dll"
		SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/w32"
		JAVA='$(shell cygpath --mixed --absolute $$JAVA_HOME)/bin/java'
		JAVAC='$(shell cygpath --mixed --absolute $$JAVA_HOME)/bin/javac'
		JAR='$(shell cygpath --mixed --absolute $$JAVA_HOME)/bin/jar'
		PATH_SEP=';'
		
		# override Linux-like directory structure
		bindir="${exec_prefix}"
		sbindir="${exec_prefix}"
		libexecdir="${exec_prefix}"
		libdir="${exec_prefix}"
	;;
esac
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(INSTALL_EXE_OPTS)
AC_SUBST(LOG4NET_DIR)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(PLATFORM_SUBDIRS)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(SEP)
AC_SUBST(SQLITELIB)
AC_SUBST(SQLITELIB_PATH)
AC_SUBST(JAVA)
AC_SUBST(JAVAC)
AC_SUBST(JAR)
AC_SUBST(PATH_SEP)

#
# Run standard macros
#
#AM_PROG_CC_STDC
#AC_PROG_INSTALL
#AC_HEADER_STDC
#AM_PROG_LIBTOOL

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  JAVACFLAGS="-g"
else
  # Build optimized version.
  JAVACFLAGS=""
fi

AC_SUBST(JAVACFLAGS)

#
# Configure files
#
AC_OUTPUT([
Makefile
ifolder3-web-java.pc
src/Makefile
src/Proxy/Makefile
src/Client/Makefile
src/Client/iFolderWebClientJava
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/linux/ifolder3-web-java.spec
])
